plugins {
    id "io.quarkus" apply false
    id "org.kordamp.gradle.jandex" apply false
}

allprojects {
    group "fr.lavachequicode"
    version "999-SNAPSHOT"
}

subprojects {
    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    test {
        systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
        useJUnitPlatform()
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs << "-parameters"
    }

    compileTestJava {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
        constraints {
            implementation "io.temporal:temporal-sdk:1.24.3"
            implementation "io.temporal:temporal-testing:1.24.3"
            testImplementation "io.temporal:temporal-testing:1.24.3"
        }
    }

    tasks {
        withType(JavaCompile).configureEach {
            options.compilerArgs += "--enable-preview"
        }

        withType(Test).configureEach {
            jvmArgs += "--enable-preview"
        }

        withType(JavaExec).configureEach {
            jvmArgs += "--enable-preview"
        }
    }
}